/*
* Copyright (c) 2025, WSO2 LLC. (http://www.wso2.com)
*
* WSO2 LLC. licenses this file to you under the Apache License,
* Version 2.0 (the "License"); you may not use this file except
* in compliance with the License.
* You may obtain a copy of the License at
*
*    http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing,
* software distributed under the License is distributed on an
* "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
* KIND, either express or implied. See the License for the
* specific language governing permissions and limitations
* under the License.
*/

plugins {
    id 'distribution'
    id 'maven-publish'
}

def componentsDir = project(':websubhub-workspace').projectDir
def extDir = file('extensions')

// Function to parse Ballerina.toml and extract main class
def extractMainClass(String ballerinatomlPath) {
    def tomlFile = file(ballerinatomlPath)
    if (!tomlFile.exists()) {
        throw new GradleException("Ballerina.toml not found at: ${ballerinatomlPath}")
    }

    def org = null
    def name = null
    def version = null

    tomlFile.eachLine { line ->
        if (line =~ /^\s*org\s*=/) {
            org = line.replaceAll(/.*=\s*"([^"]+)".*/, '$1').trim()
        } else if (line =~ /^\s*name\s*=/) {
            name = line.replaceAll(/.*=\s*"([^"]+)".*/, '$1').trim()
        } else if (line =~ /^\s*version\s*=/) {
            version = line.replaceAll(/.*=\s*"([^"]+)".*/, '$1').trim()
        }
    }

    if (!org || !name || !version) {
        throw new GradleException("Failed to parse org, name, or version from ${ballerinatomlPath}")
    }

    // Extract major version
    def majorVersion = version.split('\\.')[0]

    // Construct main class: <org>.<name>.<major-version>.$__init
    return "${org}.${name}.${majorVersion}.\$__init"
}

// Task to process websubhub scripts
task processHubScripts(type: Copy) {
    from('scripts/websubhub')
    into("${buildDir}/processed-scripts/websubhub")
    def mainClass = extractMainClass("${componentsDir}/websubhub/Ballerina.toml")
    filter { line ->
        line.replace('@MAIN_CLASS@', mainClass)
    }
}

// Task to process consolidator scripts
task processConsolidatorScripts(type: Copy) {
    from('scripts/websubhub-consolidator')
    into("${buildDir}/processed-scripts/websubhub-consolidator")
    def mainClass = extractMainClass("${componentsDir}/websubhub-consolidator/Ballerina.toml")
    filter { line ->
        line.replace('@MAIN_CLASS@', mainClass)
    }
}

distributions {
    hub {
        distributionBaseName = 'wso2websubhub'
        def confDir = file('conf/websubhub')

        contents {
            from("${componentsDir}/websubhub/target/bin") {
                include "*.jar"
                rename { fileName ->
                    fileName.replace('.jar', "-${project.version}.jar")
                }
                into "lib"
            }

            from("${confDir}") {
                include "Config.toml"
                into "conf"
            }

            from("${componentsDir}/websubhub/resources") {
                into "conf/resources"
            }

            from("${buildDir}/processed-scripts/websubhub") {
                include "*.sh", "*.bat"
                into "bin"
                fileMode = 0755
            }

            from(rootProject.projectDir) {
                include "LICENSE*"
                into ""
            }

            from (extDir) {
                into "wso2/extensions"
            }
        }
    }

    consolidator {
        distributionBaseName = 'wso2websubhub-consolidator'
        def confDir = file('conf/websubhub-consolidator')

        contents {
            from("${componentsDir}/websubhub-consolidator/target/bin") {
                include "*.jar"
                rename { fileName ->
                    fileName.replace('.jar', "-${project.version}.jar")
                }
                into "lib"
            }

            from("${confDir}") {
                include "Config.toml"
                into "conf"
            }

            from("${componentsDir}/websubhub-consolidator/resources") {
                into "conf/resources"
            }

            from("${buildDir}/processed-scripts/websubhub-consolidator") {
                include "*.sh", "*.bat"
                into "bin"
                fileMode = 0755
            }

            from(rootProject.projectDir) {
                include "LICENSE*"
                into ""
            }

            from (extDir) {
                into "wso2/extensions"
            }
        }
    }

    websubhubDistribution {
        distributionBaseName = 'wso2websubhub-distribution'
        contents {
            from(zipTree(hubDistZip.archiveFile)) {
                into ""
            }

            from(zipTree(consolidatorDistZip.archiveFile)) {
                into ""
            }

            from(projectDir) {
                include "README.txt"
                filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [version: project.version.toString().replace('-SNAPSHOT', '')])
                into ""
            }

            from(rootProject.projectDir) {
                include "LICENSE*"
                into ""
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.property('group')
            artifactId = 'wso2websubhub-distribution'
            artifact(tasks.getByName("websubhubDistributionDistZip"))
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/wso2/product-integrator-websubhub")
            credentials {
                username = System.getenv("publishUser")
                password = System.getenv("publishPAT")
            }
        }
    }
}

hubDistZip.dependsOn(':websubhub-workspace:build', processHubScripts)
hubDistTar.enabled = false

consolidatorDistZip.dependsOn(':websubhub-workspace:build', processConsolidatorScripts)
consolidatorDistTar.enabled = false

websubhubDistributionDistZip.dependsOn(hubDistZip)
websubhubDistributionDistZip.dependsOn(consolidatorDistZip)
websubhubDistributionDistTar.enabled = false

build.dependsOn(hubDistZip, consolidatorDistZip, websubhubDistributionDistZip)
